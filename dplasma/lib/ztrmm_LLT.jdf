extern "C" %{
/*
 * Copyright (c) 2010-2013 The University of Tennessee and The University
 *                         of Tennessee Research Foundation. All rights
 *                         reserved.
 * Copyright (c) 2013      Inria. All rights reserved.
 * $COPYRIGHT
 *
 * @precisions normal z -> s d c
 *
 */
#include "dplasmajdf.h"
#include "data_dist/matrix/matrix.h"

%}

side      [type = "PLASMA_enum"]
uplo      [type = "PLASMA_enum"]
trans     [type = "PLASMA_enum"]
diag      [type = "PLASMA_enum"]
alpha     [type = "dague_complex64_t"]
dataA     [type = "dague_ddesc_t *"]
descA     [type = "tiled_matrix_desc_t" hidden = on default = "*((tiled_matrix_desc_t*)dataA)"]
dataB     [type = "dague_ddesc_t *"]
descB     [type = "tiled_matrix_desc_t" hidden = on default = "*((tiled_matrix_desc_t*)dataB)"]

read_A(m, k) [profile = off]
  /* Execution Space */
  m = 0 .. (descB.mt-1)
  k = m .. (descA.mt-1)

  /* Locality */
  : dataA(k,m)

  READ  A    <- dataA(k,m)
             -> (m == k) ? A ztrmm(m, 0..(descB.nt-1))
             -> (m != k) ? A zgemm(m, 0..(descB.nt-1), k)

BODY
{
    /* nothing */
}
END

read_B(n, k) [profile = off]
  /* Execution Space */
  n = 0..(descB.nt-1)
  k = 1..(descB.mt-1)

  /* Locality */
  : dataB(k,n)

  READ  B    <- dataB(k,n)
             -> B zgemm(0..(k-1), n, k)

BODY
{
    /* nothing */
}
END

ztrmm(m, n)
  /* Execution Space */
  m = 0..(descB.mt-1)
  n = 0..(descB.nt-1)

  /* Locality */
  : dataB(m,n)

  CTL   ctl0 <- ctl0 zgemm(0..(m-1), n, m)
  READ  A    <- A read_A(m, m)
  RW    B    <- dataB(m,n)
             -> (m == (descB.mt-1)) ? dataB(m,n)
             -> (m <  (descB.mt-1)) ? C zgemm(m, n, m+1)

BODY
{
    int tempmm = ((m)==(descB.mt-1)) ? (descB.m-(m*descB.mb)) : descB.mb;
    int tempnn = ((n)==(descB.nt-1)) ? (descB.n-(n*descB.nb)) : descB.nb;
    int lda = BLKLDD( descA, m );
    int ldb = BLKLDD( descB, m );

    printlog("CORE_ztrmm(%d, %d)\n"
             "\t(side, uplo, trans, diag, tempmm, tempnn, alpha, A(%d,%d)[%p], lda, B(%d,%d)[%p], ldb)\n",
             m, n, m, m, A, m, n, B);

#if !defined(DAGUE_DRY_RUN)
    CORE_ztrmm(side, uplo, trans,
               diag, tempmm, tempnn,
               alpha, A /* dataA(m,m) */, lda,
                      B /* dataB(m,n) */, ldb );
#endif  /* !defined(DAGUE_DRY_RUN) */
}
END

zgemm(m, n, k)
  /* Execution Space */
  m = 0..(descB.mt-2)
  n = 0..(descB.nt-1)
  k = (m+1)..(descB.mt-1)

  /* Locality */
  : dataB(m,n)

  CTL   ctl0 -> ctl0 ztrmm(k, n)
  READ  A    <- A read_A(m, k)
  READ  B    <- B read_B(n, k)
  RW    C    <- (k >  (m+1)) ? C zgemm(m, n, k-1)
             <- (k == (m+1)) ? B ztrmm(k-1, n)
             -> (k == (descB.mt-1)) ? dataB(m,n)
             -> (k <  (descB.mt-1)) ? C zgemm(m, n, k+1)

BODY
{
    int tempmm = ((m)==(descB.mt-1)) ? (descB.m-(m*descB.mb)) : descB.mb;
    int tempnn = ((n)==(descB.nt-1)) ? (descB.n-(n*descB.nb)) : descB.nb;
    int tempkm = ((k)==(descA.mt-1)) ? (descA.m-(k*descA.mb)) : descA.mb;
    int ldak = BLKLDD( descA, k );
    int ldbk = BLKLDD( descB, k );
    int ldb = BLKLDD( descB, m );

    printlog("CORE_zgemm(%d, %d, %d)\n"
             "\t(trans, PlasmaNoTrans, tempmm, tempnn, tempkm, alpha, A(%d,%d)[%p], ldak, B(%d,%d)[%p], ldbk, 1.000000, B(%d,%d)[%p], ldb)\n",
             m, n, k, k, m, A, k, n, B, m, n, C);

#if !defined(DAGUE_DRY_RUN)
    CORE_zgemm(trans, PlasmaNoTrans, tempmm, tempnn, tempkm,
               alpha, A /* dataA(k,m) */, ldak,
                      B /* dataB(k,n) */, ldbk,
               1.0,   C /* dataB(m,n) */, ldb );
#endif  /* !defined(DAGUE_DRY_RUN) */
}
END
