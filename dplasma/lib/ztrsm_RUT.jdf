extern "C" %{
  /**
   * PLASMA include for defined and constants.
   *
   * @precisions normal z -> s d c
   *
   */
  
#include <plasma.h>
#include <core_blas.h>

#include "dague.h"
#include "data_distribution.h"
#include "dplasma/lib/dplasmajdf.h"

%}

/* Globals
 * GRIDrows, GRIDcols, NB, SIZE
 */
side [type = int]
uplo [type = int]
trans[type = int]
diag [type = int]

alpha[type = Dague_Complex64_t]

Am   [type = int]
An   [type = int]
Amb  [type = int]
Anb  [type = int]
Amt  [type = int]
Ant  [type = int]

Bm   [type = int]
Bn   [type = int]
Bmb  [type = int]
Bnb  [type = int]
Bmt  [type = int]
Bnt  [type = int]
     
/**************************************************
 *                      TRSM                      *
 **************************************************/
TRSM_IN(k)  [profile = off]
k = 0..Bnt-1

:A(k, k)

READ A <- A(k, k)
  -> A TRSM(0..Bmt-1, k)
BODY
 /* Nothing */
END

TRSM(m, k)

// Execution space
m = 0..Bmt-1
k = 0..Bnt-1

// Parallel partitioning
: B(m, k)

// Parameters
READ  A <- A TRSM_IN(k)
RW    B <- (k == Bnt-1) ? B(m, k) : C GEMM(k+1, m, k)
        -> B GEMM(k, m, 0..k-1)
        -> B(m, k)

BODY
      int tempmm = m == Bmt-1 ? Bm - m * Bmb : Bmb;
      int tempkn = k == Bnt-1 ? Bn - k * Bnb : Bnb;
      DRYRUN(
	   CORE_ztrsm(
			side, uplo, trans, diag,
                        tempmm, tempkn,
			alpha, A /*A(k, k)*/, Amb,
                               B /*B(m, k)*/, Bmb)
	   );

      printlog("thread %d CORE_ztrsm( %d, %d)\n\t( %s, %s, %s, %s, %d, %d, %f, A(%d,%d)[%p], %d, B(%d,%d)[%p], %d)\n",
	       context->eu_id, m, k,
               plasma_const( side ), plasma_const( uplo ), plasma_const( trans ), plasma_const( diag ),
               tempmm, tempkn,
	       alpha, k, k, A /*A(k, k)*/, Amb,
                      m, k, B /*B(m, k)*/, Bmb);
END

/**************************************************
 *                      GEMM                      *
 **************************************************/
GEMM_IN(k, n)  [profile = off]
k = 0..Bnt-1
n = 0..k-1

:A(n, k)
READ A <- A(n, k)
        -> A GEMM(k, 0..Bmt-1, n)
BODY
 /* Nothing */
END

// Name
GEMM(k, m, n)

// Execution space
k = 0..Bnt-1
m = 0..Bmt-1
n = 0..k-1

// Parallel partitioning
: B(m, n)

// Parameters
READ  A <- A GEMM_IN(k, n)   /* It's A(n,k) but we need to swap parameter for the parser */
READ  B <- B TRSM(m, k)
RW    C <- (k == Bnt-1) ? B(m, n)      : C GEMM(k+1, m, n)
        -> (k-1 == n)   ? B TRSM(m, n) : C GEMM(k-1, m, n)

BODY
      Dague_Complex64_t minvalpha  = (Dague_Complex64_t)-1.0 / alpha;
      int tempmm = m == Bmt-1 ? Bm - m * Bmb : Bmb;
      int tempkn = k == Bnt-1 ? Bn - k * Bnb : Bnb;
      DRYRUN(
	   CORE_zgemm(
			PlasmaNoTrans, trans,
                        tempmm, Bnb, tempkn,
			minvalpha,              B /* B(m, k)*/ , Bmb,
			                        A /* A(n, k)*/ , Amb,
			(Dague_Complex64_t)1.0, C /* B(m, n)*/ , Bmb)
	   );

      printlog("thread %d CORE_zgemm( %d, %d, %d )\n\t( %s, %s, %d, %d, %d, %f, A(%d,%d)[%p], %d, B(%d,%d)[%p], %d, %f, B(%d,%d)[%p], %d)\n",
	       context->eu_id, k, m, n,
               plasma_const( PlasmaNoTrans ), plasma_const( trans ), 
               tempmm, Bnb, tempkn,
	       minvalpha,   m, k, B, Bmb,
                            n, k, A, Amb,
	       (Dague_Complex64_t)1.0, m, n, C, Bmb);
END
