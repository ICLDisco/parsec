extern "C" %{
  /**
   * PLASMA include for defined and constants.
   *
   * @precisions normal z -> c d s
   *
   */
#include <plasma.h>
#include <core_blas.h>

#define PRECISION_z
#define BLKLDD(_desc, _k) (_desc).mb

#include "dague.h"
#include "data_distribution.h"
#include "dplasmajdf.h"
#include "data_dist/matrix/matrix.h"

%}

/*
 * Globals
 */
uplo   [type = PLASMA_enum]
bump   [type = Dague_Complex64_t]
seed   [type = "unsigned long long int" ]
descA  [type = "tiled_matrix_desc_t"]
A      [type = "dague_ddesc_t *"]

plasma_upper   [type="PLASMA_enum" hidden=on default=PlasmaUpper ]
plasma_lower   [type="PLASMA_enum" hidden=on default=PlasmaLower ]

PLGSY_L(m, n)

// Execution space
m = 1 .. ((uplo == plasma_upper) ? 0 : descA.mt-1)
n = 0 .. ( m < descA.nt ? m-1 : descA.nt-1 )

// Parallel partitioning
: A(m, n)

// Parameters

RW A <- A(m, n)
     -> A(m, n)
BODY
  int tempmm = ((m)==((descA.mt)-1)) ? ((descA.m)-(m*(descA.mb))) : (descA.mb);
  int tempnn = ((n)==((descA.nt)-1)) ? ((descA.n)-(n*(descA.nb))) : (descA.nb);
  int ldam = BLKLDD( descA, m );

        DRYRUN(
               CORE_zplgsy( 
                   bump, tempmm, tempnn, A, ldam,
                   descA.m, m*descA.mb, n*descA.nb, seed );
               );

        printlog("thread %d plgsy( %d, %d )\n"
                 "    ( %f, %d, %d, A(%d,%d), %d, %d, %d, %d, %lu)\n",
                 context->eu_id, m, n, tempmm, tempnn, 
                 bump, m, n, ldam,
                 descA.m, m*descA.mb, n*descA.nb, seed);
END

PLGSY_U(m, n)

// Execution space
m = 0   .. descA.mt-1
n = m+1 .. ((uplo == plasma_lower) ? 0 : descA.nt-1)

// Parallel partitioning
: A(m, n)

// Parameters

RW   A <- A(m, n)
       -> A(m, n)
BODY
  int tempmm = ((m)==((descA.mt)-1)) ? ((descA.m)-(m*(descA.mb))) : (descA.mb);
  int tempnn = ((n)==((descA.nt)-1)) ? ((descA.n)-(n*(descA.nb))) : (descA.nb);
  int ldam = BLKLDD( descA, m );

        DRYRUN(
               CORE_zplgsy( 
                   bump, tempmm, tempnn, A, ldam,
                   descA.m, m*descA.mb, n*descA.nb, seed );
               );

        printlog("thread %d plgsy( %d, %d )\n"
                 "    ( %f, %d, %d, A(%d,%d), %d, %d, %d, %d, %lu)\n",
                 context->eu_id, m, n, tempmm, tempnn, 
                 bump, m, n, ldam,
                 descA.m, m*descA.mb, n*descA.nb, seed);
END

PLGSY_DIAG(k)

// Execution space
     k = 0 .. ( descA.mt < descA.nt ? descA.mt-1 : descA.nt-1 )

// Parallel partitioning
: A(k, k)

// Parameters

RW   A <- A(k, k)
       -> A(k, k)
BODY
  int tempkm = ((k)==((descA.mt)-1)) ? ((descA.m)-(k*(descA.mb))) : (descA.mb);
  int tempkn = ((k)==((descA.nt)-1)) ? ((descA.n)-(k*(descA.nb))) : (descA.nb);
  int ldak = BLKLDD( descA, k );

        DRYRUN(
               CORE_zplgsy( 
                   bump, tempkm, tempkn, A, ldak,
                   descA.m, k*descA.mb, k*descA.nb, seed );
               );

        printlog("thread %d plgsy( %d, %d )\n"
                 "    ( %f, %d, %d, A(%d,%d), %d, %d, %d, %d, %lu)\n",
                 context->eu_id, k, k, tempkm, tempkn, 
                 bump, k, k, ldak,
                 descA.m, k*descA.mb, k*descA.nb, seed);
END
extern "C" %{

%}
