extern "C" %{
/*
 *  Copyright (c) 2010
 *
 *  The University of Tennessee and The University
 *  of Tennessee Research Foundation.  All rights
 *  reserved.
 *
 * @precisions normal z -> s d c
 *
 */
#define PRECISION_z

#include <plasma.h>
#include <core_blas.h>

#include "dague.h"
#include "data_distribution.h"
#include "data_dist/matrix/precision.h"
#include "data_dist/matrix/matrix.h"
#include "dplasma/lib/dplasmajdf.h"

#define BLKLDD( _desc_, _m_ ) ( (_desc_).storage == matrix_Tile ? (_desc_).mb : (_desc_).lm )

%}

/*
 * Globals
 */
transA [type = int]
transB [type = int]

alpha  [type = Dague_Complex64_t]
beta   [type = Dague_Complex64_t]

descA [type = "tiled_matrix_desc_t"]
A     [type = "dague_ddesc_t *"]
descB [type = "tiled_matrix_desc_t"]
B     [type = "dague_ddesc_t *"]
descC [type = "tiled_matrix_desc_t"]
C     [type = "dague_ddesc_t *"]

/**************************************************
 *                       READ_A                   *
 **************************************************/
READ_A(k, m)  [profile = off]

k = 0 .. descA.mt-1
m = 0 .. descA.nt-1

: A(k, m)

A <- A(k, m)
  -> A GEMM(m, 0, k)
BODY
    printlog("rank %u <- A(%d,%d)\n", __dague_object->super.A->myrank, k, m);
END

/**************************************************
 *                       READ_B                   *
 **************************************************/
READ_B(k, n)  [profile = off]

k = 0 .. descB.mt-1
n = 0 .. descB.nt-1

: B(k, n)

B <- B(k, n)
  -> B GEMM(0, n, k)
BODY
     printlog("rank %u <- B(%d,%d)\n", __dague_object->super.B->myrank, k, n);
END

/**************************************************
 *                       GEMM                     *
 **************************************************/
GEMM(m, n, k)

// Execution space
m = 0 .. descC.mt-1
n = 0 .. descC.nt-1
k = 0 .. descA.mt-1

// Parallel partitioning
: C(m, n)

// Parameters
READ A <- (n == 0)            ? A READ_A(k, m) : A GEMM( m, (n+descC.nt-1) % descC.nt, k )
       -> (n <= (descC.nt-2)) ? A GEMM( m, (n+1) % descC.nt, k )
READ B <- (m == 0)            ? B READ_B(k, n) : B GEMM( (m+descC.mt-1) % descC.mt, n, k )
       -> (m <= (descC.mt-2)) ? B GEMM( (m+1) % descC.mt, n, k )
RW   C <- (k == 0)            ? C(m, n) : C GEMM( m, n, k-1 )
       -> (k == (descA.mt-1)) ? C(m, n) : C GEMM( m, n, k+1 )
BODY
        Dague_Complex64_t lbeta = (k == 0) ? beta : (Dague_Complex64_t)1.0;
        int tempmm = m == descC.mt-1 ? descC.m - m * descC.mb : descC.mb;
        int tempnn = n == descC.nt-1 ? descC.n - n * descC.nb : descC.nb;
        int tempkk = k == descA.mt-1 ? descA.m - k * descA.nb : descA.nb;
        int ldak = BLKLDD(descA, k);
        int ldbk = BLKLDD(descB, k);
        int ldcm = BLKLDD(descC, m);

        DRYRUN(
            CORE_zgemm(
                transA, transB,
                tempmm, tempnn, tempkk,
                alpha, A /*A(k, m)*/, ldak,
                       B /*B(k, n)*/, ldbk,
                lbeta, C /*C(m, n)*/, ldcm);
            );

        printlog("thread %d VP %d gemm( %d, %d, %d )\n"
                 "    ( %s, %s, %d, %d, %d, %f, A(%d,%d), %d, B(%d,%d), %d, %f, C(%d,%d), %d)\n",
                 context->th_id, context->virtual_process->vp_id, m, n, k,
                 plasma_const( transA ), plasma_const( transB ), 
                 tempmm, tempnn, tempkk,
                 creal(alpha), k, m, ldak,
                               k, n, ldbk,
                 creal(lbeta), m, n, ldcm);
END

extern "C" %{

%}
