extern "C" %{
/*
 * Copyright (c) 2010-2013 The University of Tennessee and The University
 *                         of Tennessee Research Foundation. All rights
 *                         reserved.
 * Copyright (c) 2013      Inria. All rights reserved.
 * $COPYRIGHT
 *
 *
 * @precisions normal z -> s d c
 *
 */
#include "dplasma/lib/dplasmajdf.h"
#include "data_dist/matrix/matrix.h"

%}

side      [type = "PLASMA_enum"]
trans     [type = "PLASMA_enum"]
descA     [type = "parsec_tiled_matrix_dc_t*"]
descB     [type = "parsec_tiled_matrix_dc_t*"]
descT     [type = "parsec_tiled_matrix_dc_t*"]
ib        [type = "int" hidden = on default = "descT->mb" ]
KT        [type = "int" hidden = on default = "dplasma_imin(descA->mt-2, descA->nt-1)" ]
pool_0    [type = "parsec_memory_pool_t *" size = "((sizeof(parsec_complex64_t))*ib)*descT->nb"]


zunmqr(k, m)
  /* Execution Space */
  k = 0 .. descA->nt-1
  m = 0 .. descB->mt-1

  /* Locality */
  : descB(m,k)

  READ  A    <- A zunmqr_in_data_A0(k)   [type = LOWER_TILE]
  READ  T    <- T zunmqr_in_data_T1(k)   [type = LITTLE_T]
  RW    C    -> ( k == 0 ) ? descB(m,k)
             -> ( k >  0 ) ? A2 ztsmqr(k-1, k, m)
             <- ( k <  (descB->nt-1)) ? A1 ztsmqr(k, k+1, m)
             <- ( k == (descB->nt-1)) ? descB(m,k)

BODY
{
    int tempmm   = (m == (descB->mt-1)) ? (descB->m - m * descB->mb) : descB->mb;
    int tempkn   = (k == (descB->nt-1)) ? (descB->n - k * descB->nb) : descB->nb;
    int tempkmin = (k == (descA->nt-1)) ? (descA->n - k * descA->nb) : descA->nb;
    int ldak = BLKLDD( descA, k );
    int ldbm = BLKLDD( descB, m );

    printlog("CORE_zunmqr(%d, %d)\n"
             "\t(side, trans, tempmm, tempkn, tempkmin, ib, A(%d,%d)[%p], ldak, T(%d,%d)[%p], descT->mb, B(%d,%d)[%p], ldbm, p_elem_A, descT->nb)\n",
             k, m, k, k, A, k, k, T, m, k, C);

#if !defined(PARSEC_DRY_RUN)
    void *p_elem_A = parsec_private_memory_pop( pool_0 );

    CORE_zunmqr(side, trans,
                tempmm, tempkn, tempkmin, ib,
                A /* descA(k,k) */, ldak,
                T /* descT(k,k) */, descT->mb,
                C /* descB(m,k) */, ldbm,
                p_elem_A, descT->nb );

    parsec_private_memory_push( pool_0, p_elem_A );
#endif  /* !defined(PARSEC_DRY_RUN) */
}
END

zunmqr_in_data_T1(k) [profile = off]
  /* Execution Space */
  k = 0 .. descA->nt-1

  /* Locality */
  : descT(k, k)

  READ  T    <- descT(k,k)                      [type = LITTLE_T]
             -> T zunmqr(k, 0 .. descB->mt-1)    [type = LITTLE_T]

BODY
{
    /* nothing */
}
END

zunmqr_in_data_A0(k) [profile = off]
  /* Execution Space */
  k = 0 .. descA->nt-1

  /* Locality */
  : descA(k, k)

  READ  A    <- descA(k,k)                      [type = LOWER_TILE]
             -> A zunmqr(k, 0 .. (descB->mt-1))  [type = LOWER_TILE]

BODY
{
    /* nothing */
}
END

ztsmqr(k, n, m)
  /* Execution Space */
  k = 0     .. KT
  n = (k+1) .. (descB->nt-1)
  m = 0     .. (descB->mt-1)

  /* Locality */
  : descB(m,n)

  RW    A1   -> ( n == (k+1) ) ? C  zunmqr(k, m)
             -> ( n >  (k+1) ) ? A1 ztsmqr(k, n-1, m)
             <- ( n <  (descB->nt-1) ) ? A1 ztsmqr(k, n+1, m)
             <- ( n == (descB->nt-1) ) ? A1 ztsmqr_out_data_B0(k, m)
  RW    A2   -> ( k == 0 ) ? descB(m,n)
             -> ( k >  0 ) ? A2 ztsmqr(k-1, n, m)
             <-  (k == (descA->nt-1)) ? descB(m,n)
             <- ((k <  (descA->nt-1)) & (n == (k+1))) ? C  zunmqr(k+1, m)
             <- ((k <  (descA->nt-1)) & (n >  (k+1))) ? A2 ztsmqr(k+1, n, m)
  READ  V    <- V ztsmqr_in_data_A1(k, n)
  READ  T    <- T ztsmqr_in_data_T2(k, n)  [type = LITTLE_T]

BODY
{
    int tempnn   = (n == (descB->nt-1)) ? (descB->n - n * descB->nb) : descB->nb;
    int tempmm   = (m == (descB->mt-1)) ? (descB->m - m * descB->mb) : descB->mb;
    int tempkmin = (k == (descA->nt-1)) ? (descA->n - k * descA->nb) : descA->nb;
    int ldbm = BLKLDD( descB, m );
    int ldan = BLKLDD( descA, n );
    int ldwork = descT->nb;

    printlog("CORE_ztsmqr(%d, %d, %d)\n"
             "\t(side, trans, tempmm, descB->nb, tempmm, tempnn, tempkmin, ib, B(%d,%d)[%p], ldbm, B(%d,%d)[%p], ldbm, A(%d,%d)[%p], ldan, T(%d,%d)[%p], descT->mb, p_elem_A, ldwork)\n",
             k, n, m, m, k, A1, m, n, A2, n, k, V, n, k, T);

#if !defined(PARSEC_DRY_RUN)
    void *p_elem_A = parsec_private_memory_pop( pool_0 );

    CORE_ztsmqr(side, trans,
                tempmm, descB->nb, tempmm, tempnn, tempkmin, ib,
                A1 /* descB(m,k) */, ldbm,
                A2 /* descB(m,n) */, ldbm,
                V  /* descA(n,k) */, ldan,
                T  /* descT(n,k) */, descT->mb,
                p_elem_A, ldwork );

    parsec_private_memory_push( pool_0, p_elem_A );
#endif  /* !defined(PARSEC_DRY_RUN) */
}
END

ztsmqr_in_data_T2(k, n) [profile = off]
  /* Execution Space */
  k = 0     .. KT
  n = (k+1) .. (descB->nt-1)

  /* Locality */
  : descT(n,k)

  READ  T    <- descT(n,k)                        [type = LITTLE_T]
             -> T ztsmqr(k, n, 0 .. descB->mt-1)   [type = LITTLE_T]

BODY
{
    /* nothing */
}
END

ztsmqr_in_data_A1(k, n) [profile = off]
  /* Execution Space */
  k = 0     .. KT
  n = (k+1) .. (descB->nt-1)

  /* Locality */
  : descA(n,k)

  READ  V    <- descA(n,k)
             -> V ztsmqr(k, n, 0 .. descB->mt-1)

BODY
{
    /* nothing */
}
END

ztsmqr_out_data_B0(k, m) [profile = off]
  /* Execution Space */
  k = 0 .. KT
  m = 0 .. (descB->mt-1)

  /* Locality */
  : descB(m,k)

  READ  A1   -> A1 ztsmqr(k, descB->nt-1, m)
             <- descB(m,k)

BODY
{
    /* nothing */
}
END
