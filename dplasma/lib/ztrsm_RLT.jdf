extern "C" %{
/*
 *  Copyright (c) 2010-2013
 *
 *  The University of Tennessee and The University
 *  of Tennessee Research Foundation.  All rights
 *  reserved.
 *
 * @precisions normal z -> s d c
 *
 */
#include "dplasma/lib/dplasmajdf.h"
#include "parsec/data_dist/matrix/matrix.h"

%}

side      [type = "PLASMA_enum"]
uplo      [type = "PLASMA_enum"]
trans     [type = "PLASMA_enum"]
diag      [type = "PLASMA_enum"]
alpha     [type = "parsec_complex64_t"]
descA     [type = "const parsec_tiled_matrix_dc_t*"]
descB     [type = "parsec_tiled_matrix_dc_t*"]


ztrsm(k,m)
  /* Execution space */
  k = 0 .. (descB->nt-1)
  m = 0 .. (descB->mt-1)

  : descB(m,k)

  READ  A <- A ztrsm_in_A0(k)

  RW    B <- (0==k) ? descB(m,k)
          <- (k>=1) ? E zgemm(k-1, m, k)
          -> (descB->nt>=(k+2)) ? C zgemm(k, m, (k+1) .. (descB->nt-1))
          -> descB(m,k)

BODY
{
    int tempmm = ((m)==(descB->mt-1)) ? (descB->m-(m*descB->mb)) : descB->mb;
    int tempkn = ((k)==(descB->nt-1)) ? (descB->n-(k*descB->nb)) : descB->nb;
    int lda = BLKLDD( descA, k );
    int ldb = BLKLDD( descB, m );

#if !defined(PARSEC_DRY_RUN)
        CORE_ztrsm(side, uplo, trans, diag,
                   tempmm, tempkn, alpha,
                   A /* descA(k,k) */, lda,
                   B /* descB(m,k) */, ldb );
#endif /* !defined(PARSEC_DRY_RUN) */

    printlog("CORE_ztrsm(%d, %d)\n"
             "\t(side, uplo, trans, diag, tempmm, tempkn, alpha, descA(%d,%d)[%p], lda, descB(%d,%d)[%p], ldb)\n",
             k, m, k, k, A, m, k, B);
}
END

/*
 * Pseudo-task
 */
ztrsm_in_A0(k) [profile = off]
  k = 0 .. (descB->nt-1)

  : descA(k,k)

  RW A <- descA(k,k)
       -> A ztrsm(k,0 .. (descB->mt-1))
BODY
{
    /* nothing */
}
END


zgemm(k,m,n)
  /* Execution space */
  k = 0 .. (descB->nt-2)
  m = 0 .. (descB->mt-1)
  n = (k+1) .. (descB->nt-1)

  : descB(m,n)

  READ  C <- B ztrsm(k, m)
  READ  D <- D zgemm_in_A0(k,n)

  RW    E <- (0==k) ? descB(m,n)
          <- (k>=1) ? E zgemm(k-1, m, n)
          -> ((1+k)==n) ? B ztrsm(n, m)
          -> (n>=(2+k)) ? E zgemm(k+1, m, n)

BODY [type=CUDA
      dyld=cublasZgemm dyldtype=cublas_zgemm_t
      weight=(n+1-k)]
{
#if defined(PRECISION_z) || defined(PRECISION_c)
    cuDoubleComplex zone  = make_cuDoubleComplex( 1., 0.);
#else
    double zone = 1.;
#endif

    int tempmm = ((m)==(descB->mt-1)) ? (descB->m-(m*descB->mb)) : descB->mb;
    int tempnn = ((n)==(descB->nt-1)) ? (descB->n-(n*descB->nb)) : descB->nb;
    parsec_complex64_t minvalpha = ((parsec_complex64_t)-1.000000)/alpha;
    int ldb = BLKLDD( descB, m );
    int ldan = BLKLDD( descA, n );

    cublasStatus_t status;

    cublasSetKernelStream( parsec_body.stream );
    parsec_body.dyld_fn( 'N', lapack_const(trans),
                         tempmm, tempnn, descB->mb,
                         minvalpha, (cuDoubleComplex*)C, ldb,
                                    (cuDoubleComplex*)D, ldan,
                         zone,      (cuDoubleComplex*)E, ldb );
    status = cublasGetError();
    PARSEC_CUDA_CHECK_ERROR( "cublasZgemm ", status,
                             {return -1;} );
}
END

BODY
{
    int tempmm = ((m)==(descB->mt-1)) ? (descB->m-(m*descB->mb)) : descB->mb;
    int tempnn = ((n)==(descB->nt-1)) ? (descB->n-(n*descB->nb)) : descB->nb;
    parsec_complex64_t minvalpha = ((parsec_complex64_t)-1.000000)/alpha;
    int ldb = BLKLDD( descB, m );
    int ldan = BLKLDD( descA, n );

#if !defined(PARSEC_DRY_RUN)
        CORE_zgemm(PlasmaNoTrans, trans,
                   tempmm, tempnn, descB->mb,
                   minvalpha, C /* descB(m,k) */, ldb,
                              D /* descA(n,k) */, ldan,
                   1.0,       E /* descB(m,n) */, ldb );
#endif /* !defined(PARSEC_DRY_RUN) */

    printlog("CORE_zgemm(%d, %d, %d)\n"
             "\t(PlasmaNoTrans, trans, tempmm, tempnn, descB->mb, minvalpha, descB(%d,%d)[%p], ldb, descA(%d,%d)[%p], ldan, zone, descB(%d,%d)[%p], ldb)\n",
             k, m, n, m, k, C, n, k, D, m, n, E);
}
END

/*
 * Pseudo-task
 */
zgemm_in_A0(k,n) [profile = off]
  k = 0 .. (descB->nt-2)
  n = (k+1) .. (descB->nt-1)

  : descA(n,k)

  RW D <- descA(n,k)
       -> D zgemm(k,0 .. (descB->mt-1),n)
BODY
{
    /* nothing */
}
END
