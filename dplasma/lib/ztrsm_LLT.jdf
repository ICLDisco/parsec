extern "C" %{
  /**
   * PLASMA include for defined and constants.
   *
   * @precisions normal z -> s d c
   *
   */
  
#include <plasma.h>
#include <core_blas.h>

#include "dague.h"
#include "data_distribution.h"
#include "dplasmajdf.h"

%}

/* Globals
 * GRIDrows, GRIDcols, NB, SIZE
 */
side [int]
uplo [int]
trans[int]
diag [int]

alpha[Dague_Complex64_t]

Am   [int]
An   [int]
Amb  [int]
Anb  [int]
Amt  [int]
Ant  [int]

Bm   [int]
Bn   [int]
Bmb  [int]
Bnb  [int]
Bmt  [int]
Bnt  [int]
     
/**************************************************
 *                      TRSM                      *
 **************************************************/
TRSM_IN(k)
k = 0..Bmt-1

:A(k, k)

RW A <- A(k, k)
     -> A TRSM(k, 0..Bnt-1)

BODY
  /* Nothing */
END

TRSM(k, n)

// Execution space
k = 0..Bmt-1
n = 0..Bnt-1

// Parallel partitioning
: B(k,n)

// Parameters
READ  A <- A TRSM_IN(k)
RW    B <- (k == Bmt-1) ? B(k, n) : C GEMM(k-1, k, n)
        -> B GEMM(k, 0..k-1, n)
        -> B(k, n)

BODY
      Dague_Complex64_t lalpha = k == Bmt-1 ? alpha : (Dague_Complex64_t)1.0;
      int tempkm = k == Bmt-1 ? Bm - k * Bmb : Bmb;
      int tempnn = n == Bnt-1 ? Bn - n * Bnb : Bnb;
      DRYRUN(
	   CORE_ztrsm(
			side, uplo, trans, diag,
			tempkm, tempnn,
			lalpha, A /*A(k, k)*/, Amb,
                                B /*B(k, n)*/, Bmb)
	   );

      printlog("thread %d CORE_ztrsm( %s, %s, %s, %s, %d, %d, %f, A(%d,%d)[%p], %d, B(%d,%d)[%p], %d)\n",
	       context->eu_id,
               plasma_const( side ), plasma_const( uplo ), 
               plasma_const( trans ), plasma_const( diag ),
               tempkm, tempnn,
	       lalpha, k, k, A /*A(k, k)*/, Amb,
                       k, n, B /*B(k, n)*/, Bmb);
END

/**************************************************
 *                      GEMM                      *
 **************************************************/
GEMM_IN(k, m)
k = 0..Bmt-1
m = 0..k-1

: A(k, m)

RW A <- A(k, m)
     -> A GEMM(k, m, 0..Bnt-1)

BODY
 /* Nothing */
END

// Name
GEMM(k, m, n)

// Execution space
k = 0..Bmt-1
m = 0..k-1
n = 0..Bnt-1

// Parallel partitioning
: B(m, n)

// Parameters
READ  A <- A GEMM_IN(k, m)
READ  B <- B TRSM(k, n)
RW    C <- (k == Bmt-1) ? B(m, n) : C GEMM(k+1, m, n)
        -> (k-1 == m)   ? B TRSM(m, n) : C GEMM(k-1, m, n)

BODY
      Dague_Complex64_t lalpha = k == Bmt-1 ? alpha : (Dague_Complex64_t)1.0;    
      int tempkm = k == Bmt-1 ? Bm - k * Bmb : Bmb;
      int tempnn = n == Bnt-1 ? Bn - n * Bnb : Bnb;
      DRYRUN(
	   CORE_zgemm(
			trans, PlasmaNoTrans,
			Bmb, tempnn, tempkm,
			(Dague_Complex64_t)-1.0, A /* A(k, m)*/ , Amb,
			              B /* B(k, n)*/ , Bmb,
			lalpha,       C /* B(m, n)*/ , Bmb)
	   );

      printlog("thread %d CORE_zgemm( %s, %s, %d, %d, %d, %f, A(%d,%d)[%p], %d, B(%d,%d)[%p], %d, %f, B(%d,%d)[%p], %d)\n",
	       context->eu_id, plasma_const( trans ), plasma_const( PlasmaNoTrans ), 
               Bmb, tempnn, tempkm,
	       (Dague_Complex64_t)-1.0, k, m, A, Amb,
                             k, n, B, Bmb,
	       lalpha,       m, n, C, Bmb);
END
