extern "C" %{
  /**
   * PLASMA include for defined and constants.
   *
   * @precisions normal z -> s d c
   *
   */
#include "dague.h"
#include <math.h>
#include <plasma.h>
#include <core_blas.h>

#include "data_distribution.h"
#include "dplasma/lib/memory_pool.h"
#include "dplasma/lib/dplasmajdf.h"
#include "data_dist/matrix/matrix.h"
#include "dplasma/lib/pivgen.h"
  
#define PRECISION_z
#define BLKLDD(_desc, _k) (_desc).mb
#define MYMIN(a, b) ((a)<(b)?(a):(b))
#define MYMAX(a, b) ((a)>(b)?(a):(b))

//#define PRIO_YVES1

#if defined(PRIO_YVES1)
#define GETPRIO_PANEL( __m, __n )      descA.mt * descA.nt - ((descA.nt - (__n) - 1) * descA.mt + (__m) + 1)
#define GETPRIO_UPDTE( __m, __n, __k ) descA.mt * descA.nt - ((descA.nt - (__n) - 1) * descA.mt + (__m) + 1)
#elif defined(PRIO_YVES2)
#define GETPRIO_PANEL( __m, __n )      descA.mt * descA.nt - ((__m) * descA.nt + descA.nt - (__n))
#define GETPRIO_UPDTE( __m, __n, __k ) descA.mt * descA.nt - ((__m) * descA.nt + descA.nt - (__n))
#elif defined(PRIO_MATHIEU1)
#define GETPRIO_PANEL( __m, __n )      (descA.mt + (__n) - (__m) - 1) * descA.nt + (__n)
#define GETPRIO_UPDTE( __m, __n, __k ) (descA.mt + (__n) - (__m) - 1) * descA.nt + (__n)
#elif defined(PRIO_MATHIEU2)
#define GETPRIO_PANEL( __m, __n )      ((MYMAX(descA.mt, descA.nt) - MYMAX( (__n) - (__m), (__m) - (__n) ) -1 ) * 12 + (__n))
#define GETPRIO_UPDTE( __m, __n, __k ) ((MYMAX(descA.mt, descA.nt) - MYMAX( (__n) - (__m), (__m) - (__n) ) -1 ) * 12 + (__n))
#elif defined(PRIO_MATYVES)
#define FORMULE( __x ) ( ( -1. + sqrt( 1. + 4.* (__x) * (__x)) ) * 0.5 )
#define GETPRIO_PANEL( __m, __k )      (int)( 22. * (__k) + 6. * ( FORMULE( descA.mt ) - FORMULE( (__m) - (__k) + 1. ) ) )
#define GETPRIO_UPDTE( __m, __n, __k ) (int)( (__m) < (__n) ? GETPRIO_PANEL( (__n), (__n) ) - 22. * ( (__m) - (__k) ) - 6. * ( (__n) - (__m) ) \
                                              :               GETPRIO_PANEL( (__m), (__n) ) - 22. * ( (__n) - (__k) ) )
#else
  /*#warning running without priority*/
#define GETPRIO_PANEL( __m, __n )      0
#define GETPRIO_UPDTE( __m, __n, __k ) 0
#endif

%}

descA  [type = "tiled_matrix_desc_t"]
A      [type = "dague_ddesc_t *"]
descT  [type = "tiled_matrix_desc_t"]
T      [type = "dague_ddesc_t *" aligned=A]
descT2 [type = "tiled_matrix_desc_t"]
T2     [type = "dague_ddesc_t *" aligned=A]
pivfct [type = "qr_piv_t*"]
ib     [type = "int"]
p_work [type = "dague_memory_pool_t *" size = "(sizeof(PLASMA_Complex64_t)*ib*(descT.nb))"]
p_tau  [type = "dague_memory_pool_t *" size = "(sizeof(PLASMA_Complex64_t)   *(descT.nb))"]
param_p[type = int default="pivfct->p" hidden=on]
param_a[type = int default="pivfct->a" hidden=on]
param_d[type = int default="pivfct->domino" hidden=on]

zgeqrt_typechange(k, i)
  /* Execution space */
  k = 0..( (descA.mt < descA.nt) ? descA.mt-1 : descA.nt-1 )
  i = 0..inline_c %{ return dplasma_qr_getnbgeqrf( param_a, param_p, param_d, k, descA.mt ) - 1; %}
  m = inline_c %{ return dplasma_qr_getm( param_a, param_p, param_d, k, i); %}
    
  : A(m, k)

  RW A <- A zgeqrt(k, i)
       -> ( descA.nt-1 > k ) ? A zunmqr(k, i, (k+1)..(descA.nt-1)) [type = LOWER_TILE]
       -> A(m, k)                                                  [type = LOWER_TILE]
BODY
 /* Nothing */
END



zgeqrt(k, i)
  /* Execution space */
  k = 0..( (descA.mt < descA.nt) ? descA.mt-1 : descA.nt-1 )
  i = 0..inline_c %{ return dplasma_qr_getnbgeqrf( param_a, param_p, param_d, k, descA.mt ) - 1; %}
  m = inline_c %{ return dplasma_qr_getm( param_a, param_p, param_d, k, i); %}
  nextm = inline_c %{ return pivfct->nextpiv( pivfct, m, k, descA.mt); %}

  SIMCOST 4

  : A(m, k)

  /* A == A(m, k) */
  /* T == T(m, k) */

  RW    A <- ( 0 == k ) ? A(m, k) 
          <- ( k > 0  ) ? A2 zttmqr(k-1, m, k ) 
          -> A zgeqrt_typechange(k, i) 
          //-> (descA.nt-1 > k) ? A zunmqr(k, i, (k+1)..(descA.nt-1)) 
          -> ( k == descA.mt-1 ) ? A(m, k)                                     [type = UPPER_TILE]
          -> ( (k < descA.mt-1) & (nextm != descA.mt) ) ?  A1 zttqrt(k, nextm) [type = UPPER_TILE]
          -> ( (k < descA.mt-1) & (nextm == descA.mt) ) ?  A2 zttqrt(k, m)     [type = UPPER_TILE]
  RW    T <- T(m, k)                                                           [type = LITTLE_T]
          -> T(m, k)                                                           [type = LITTLE_T]
          -> (descA.nt-1 > k) ? T zunmqr(k, i, (k+1)..(descA.nt-1))            [type = LITTLE_T]
       
  ; inline_c %{ return GETPRIO_PANEL(m, k); %}

BODY
  DRYRUN(
         void *p_elem_A = dague_private_memory_pop( p_tau  );
         void *p_elem_B = dague_private_memory_pop( p_work );

         int tempmm = ((m)==((descA.mt)-1)) ? ((descA.m)-(m*(descA.mb))) : (descA.mb);
         int tempkn = ((k)==((descA.nt)-1)) ? ((descA.n)-(k*(descA.nb))) : (descA.nb);
         int ldam = BLKLDD( descA, m );
         
         CORE_zgeqrt(tempmm, tempkn, ib, 
                     A /* A(m,k) */, ldam, 
                     T /* T(m,k) */, descT.mb, 
                     p_elem_A, p_elem_B );

         dague_private_memory_push( p_tau, p_elem_A );
         dague_private_memory_push( p_work, p_elem_B );

         );
                                                                    
  printlog("thread %d CORE_zgeqrt(%d, %d)\n"
           "\t(tempmm, tempkn, ib, A(%d,%d)[%p], ldam, T(%d,%d)[%p], descT.mb, p_elem_A, p_elem_B)\n",
           context->eu_id, k, m, m, k, A, m, k, T);

END


zunmqr(k, i, n)
  /* Execution space */
  k = 0..( ( descA.mt < descA.nt ) ? descA.mt-1 : descA.nt-1)
  i = 0..inline_c %{ return dplasma_qr_getnbgeqrf( param_a, param_p, param_d, k, descA.mt ) - 1; %}
  n = k+1..descA.nt-1
  m = inline_c %{ return dplasma_qr_getm( param_a, param_p, param_d, k, i); %}
  nextm = inline_c %{ return pivfct->nextpiv( pivfct, m, k, descA.mt); %}

  SIMCOST 6

  : A(m, n)

  /* A == A(m,k) */
  /* T == T(m,k) */
  /* C == A(m,n) */

  READ  A <- A zgeqrt_typechange(k, i)                              [type = LOWER_TILE]
  READ  T <- T zgeqrt(k, i)                                         [type = LITTLE_T]
  RW    C <- ( 0 == k ) ? A(m, n)
          <- ( k >  0 ) ? A2 zttmqr(k-1, m, n) 
          -> ( k == descA.mt-1 ) ? A(m, n)   
          -> ( (k < descA.mt-1) & (nextm != descA.mt) ) ? A1 zttmqr(k, nextm, n)
          -> ( (k < descA.mt-1) & (nextm == descA.mt) ) ? A2 zttmqr(k, m,     n)

    ; inline_c %{ return GETPRIO_UPDTE(m, n, k); %}

BODY
  DRYRUN(
         void *p_elem_A = dague_private_memory_pop( p_work );
         
         int tempmm = ((m)==((descA.mt)-1)) ? ((descA.m)-(m*(descA.mb))) : (descA.mb);
         int tempnn = ((n)==((descA.nt)-1)) ? ((descA.n)-(n*(descA.nb))) : (descA.nb);
         int ldam = BLKLDD( descA, m );

         CORE_zunmqr(
                     PlasmaLeft, PlasmaConjTrans, 
                     tempmm, tempnn, tempmm, ib, 
                     A /* A(m, k) */, ldam, 
                     T /* T(m, k) */, descT.mb, 
                     C /* A(m, n) */, ldam, 
                     p_elem_A, descT.nb );

         dague_private_memory_push( p_work, p_elem_A );
         );

  printlog("thread %d CORE_zunmqr(%d, %d, %d)\n"
           "\t(PlasmaLeft, PlasmaConjTrans, tempmm, tempnn, min(tempmm, tempnn), ib, \n"
           "\t A(%d,%d)[%p], ldam, T(%d,%d)[%p], descT.mb, A(%d,%d)[%p], ldam, p_elem_A, descT.nb)\n",
           context->eu_id, k, m, n, m, k, A, m, k, T, m, n, C);

END


zttqrt_out_A1(k)
  k = 0..( (descA.mt <= descA.nt) ? descA.mt-2 : descA.nt-1 )
  prevp = inline_c %{ return pivfct->prevpiv(pivfct, k, k, k); %}

  : A(k, k)
  
  RW A <- A1 zttqrt( k, prevp ) [type = UPPER_TILE]
       -> A(k, k)               [type = UPPER_TILE]
BODY
/* nothing */
END

zttqrt(k, m)
  /* Execution space */
  k = 0..( (descA.mt < descA.nt) ? descA.mt-1 : descA.nt-1 )
  m = k+1..descA.mt-1
  p =     inline_c %{ return pivfct->currpiv(pivfct, m, k); %}
  nextp = inline_c %{ return pivfct->nextpiv(pivfct, p, k, m); %}
  prevp = inline_c %{ return pivfct->prevpiv(pivfct, p, k, m); %}
  prevm = inline_c %{ return pivfct->prevpiv(pivfct, m, k, m); %}
  type  = inline_c %{ return dplasma_qr_gettype( param_a, param_p, param_d, k, m ); %}
  ip    = inline_c %{ return dplasma_qr_geti(    param_a, param_p, param_d, k, p ); %}
  im    = inline_c %{ return dplasma_qr_geti(    param_a, param_p, param_d, k, m ); %}

  SIMCOST inline_c %{ return type == 0 ? 4 : 2; %}

  : A(m, k)

  /* A1 == A(p,  k) */
  /* A2 == A(m,  k) */
  /* T  == T2(m, k) */

  RW   A1 <- (   prevp == descA.mt ) ? A  zgeqrt(k, ip ) : A1 zttqrt(k, prevp ) [type = UPPER_TILE]
          -> (   nextp != descA.mt ) ? A1 zttqrt(k, nextp )                     [type = UPPER_TILE]
          -> ( ( nextp == descA.mt ) & (p == k) ) ? A zttqrt_out_A1(k)          [type = UPPER_TILE]
          -> ( ( nextp == descA.mt ) & (p != k) ) ? A2 zttqrt(k, p)             [type = UPPER_TILE]
  RW   A2 <- ( (type == 0) && (k     == 0        ) ) ? A(m, k)
          <- ( (type == 0) && (k     != 0        ) ) ? A2 zttmqr(k-1, m, k )
          <- ( (type != 0) && (prevm == descA.mt ) ) ? A  zgeqrt(k, im )
          <- ( (type != 0) && (prevm != descA.mt ) ) ? A1 zttqrt(k, prevm )
          -> A(m, k)
          -> (descA.nt-1 > k)? V zttmqr(k, m, (k+1)..(descA.nt-1))
  RW   T  <- T2(m, k)                                              [type = LITTLE_T]
          -> T2(m, k)                                              [type = LITTLE_T]
          -> (descA.nt-1 > k)? T zttmqr(k, m, (k+1)..(descA.nt-1)) [type = LITTLE_T]

 ; inline_c %{ return type == 0 ? GETPRIO_PANEL(p, k) : GETPRIO_PANEL(m, k); %}

BODY
  DRYRUN(
         void *p_elem_A = dague_private_memory_pop( p_tau  );
         void *p_elem_B = dague_private_memory_pop( p_work );
         
         int tempmm = ((m)==((descA.mt)-1)) ? ((descA.m)-(m*(descA.mb))) : (descA.mb);
         int tempkn = ((k)==((descA.nt)-1)) ? ((descA.n)-(k*(descA.nb))) : (descA.nb);
         int ldam = BLKLDD( descA, m );
         
         if ( type == 0 ) {
           CORE_ztsqrt(
                     tempmm, tempkn, ib, 
                     A1 /* A(p, k) */, descA.mb, 
                     A2 /* A(m, k) */, ldam, 
                     T  /* T(m, k) */, descT.mb, 
                     p_elem_A, p_elem_B );
         } else {
           CORE_zttqrt(
                     tempmm, tempkn, ib, 
                     A1 /* A(p, k) */, descA.mb, 
                     A2 /* A(m, k) */, ldam, 
                     T  /* T(m, k) */, descT.mb, 
                     p_elem_A, p_elem_B );
         }
         dague_private_memory_push( p_tau , p_elem_A );
         dague_private_memory_push( p_work, p_elem_B );
         
         );

  printlog("thread %d CORE_zttqrt(%d, %d)\n"
           "\t(tempmm, tempkn, ib, A(%d,%d)[%p], A.mb, A(%d,%d)[%p], ldam, T(%d,%d)[%p], descT.mb, p_elem_A, p_elem_B)\n",
           context->eu_id, k, m, p, k, A1, m, k, A2, m, k, T);

END


zttmqr_out_A1(k, n)
  k = 0..( (descA.mt < descA.nt) ? descA.mt-2 : descA.nt-2 )
  n = k+1..descA.nt-1
  prevp = inline_c %{ return pivfct->prevpiv(pivfct, k, k, k); %}

  : A(k, n)
  
  RW A <- A1 zttmqr( k, prevp, n )
       -> A(k, n)
BODY
/* nothing */
END

zttmqr(k, m, n)
  /* Execution space */
  k = 0..( (descA.mt < descA.nt) ? descA.mt-1 : descA.nt-1 )
  m = k+1..descA.mt-1
  n = k+1..descA.nt-1
  p =     inline_c %{ return pivfct->currpiv(pivfct, m, k); %}
  nextp = inline_c %{ return pivfct->nextpiv(pivfct, p, k, m); %}
  prevp = inline_c %{ return pivfct->prevpiv(pivfct, p, k, m); %}
  prevm = inline_c %{ return pivfct->prevpiv(pivfct, m, k, m); %}
  type  = inline_c %{ return dplasma_qr_gettype( param_a, param_p, param_d, k,   m ); %}
  ip    = inline_c %{ return dplasma_qr_geti(    param_a, param_p, param_d, k,   p ); %}
  im    = inline_c %{ return dplasma_qr_geti(    param_a, param_p, param_d, k,   m ); %}
  im1   = inline_c %{ return dplasma_qr_geti(    param_a, param_p, param_d, k+1, m ); %}

  SIMCOST inline_c %{ return type == 0 ? 12 : 6; %}

  : A(m, n)

  /* V  == A(m, k) */
  /* T  == T(m, k) */
  /* A1 == A(p, n) */
  /* A2 == A(m, n) */

  RW   A1 <- (   prevp == descA.mt ) ? C  zunmqr( k, ip, n ) : A1 zttmqr(k, prevp, n )
          -> (   nextp != descA.mt ) ? A1 zttmqr( k, nextp, n)
          -> ( ( nextp == descA.mt ) & ( p == k ) ) ? A zttmqr_out_A1(p, n)
          -> ( ( nextp == descA.mt ) & ( p != k ) ) ? A2 zttmqr( k, p, n )
  RW   A2 <- ( (type == 0) && (k     == 0        ) ) ? A(m, n)
          <- ( (type == 0) && (k     != 0        ) ) ? A2 zttmqr(k-1, m, n )
          <- ( (type != 0) && (prevm == descA.mt ) ) ? C  zunmqr(k, im, n)
          <- ( (type != 0) && (prevm != descA.mt ) ) ? A1 zttmqr(k, prevm, n )
          -> ( (type == 0) && (m/param_p == (k+1)) && (n==(k+1)) ) ? A  zgeqrt( k+1, im1 ) 
          -> ( (type == 0) && (m/param_p == (k+1)) && (n>  k+1)  ) ? C  zunmqr( k+1, im1, n )
          -> ( (type == 0) && (m/param_p >  (k+1)) && (n==(k+1)) ) ? A2 zttqrt( k+1, m ) 
          -> ( (type == 0) && (m/param_p >  (k+1)) && (n> (k+1)) ) ? A2 zttmqr( k+1, m, n ) 
          -> ( (type != 0) && ( n == k+1 ) ) ? A zgeqrt( k+1, im1 ) 
          -> ( (type != 0) && ( n >  k+1 ) ) ? C zunmqr( k+1, im1, n )
  READ  V <- A2 zttqrt(k, m) 
  READ  T <- T  zttqrt(k, m) [type = LITTLE_T]

    ; inline_c %{ return type == 0 ? GETPRIO_UPDTE(p, n, k) : GETPRIO_UPDTE(m, n, k); %}

BODY
  DRYRUN(
         void *p_elem_A = dague_private_memory_pop( p_work );
         
         int tempnn = ((n)==((descA.nt)-1)) ? ((descA.n)-(n*(descA.nb))) : (descA.nb);
         int tempmm = ((m)==((descA.mt)-1)) ? ((descA.m)-(m*(descA.mb))) : (descA.mb);
         int ldam = BLKLDD( descA, m );
         int ldwork = ib;
         
         if ( type == 0 ) {
           CORE_ztsmqr(
             PlasmaLeft, PlasmaConjTrans, 
             descA.mb, tempnn, tempmm, tempnn, descA.nb, ib, 
             A1 /* A(p, n) */, descA.mb, 
             A2 /* A(m, n) */, ldam, 
             V  /* A(m, k) */, ldam, 
             T  /* T(m, k) */, descT.mb, 
             p_elem_A, ldwork );
         } else {
           CORE_zttmqr(
             PlasmaLeft, PlasmaConjTrans, 
             descA.mb, tempnn, tempmm, tempnn, descA.nb, ib, 
             A1 /* A(p, n) */, descA.mb, 
             A2 /* A(m, n) */, ldam, 
             V  /* A(m, k) */, ldam, 
             T  /* T(m, k) */, descT.mb, 
             p_elem_A, ldwork );
         }
         dague_private_memory_push( p_work, p_elem_A );
         
         );

  printlog("thread %d CORE_zttmqr(%d, %d, %d)\n"
           "\t(PlasmaLeft, PlasmaConjTrans, descA.mb, tempnn, tempmm, tempnn, descA.nb, ib, \n"
           "\t A(%d,%d)[%p], A.mb, A(%d,%d)[%p], ldam, A(%d,%d)[%p], ldam, T(%d,%d)[%p], descT.mb, p_elem_A, ldwork)\n",
           context->eu_id, k, m, n, p, n, A1, m, n, A2, m, k, V, m, k, T);

END
