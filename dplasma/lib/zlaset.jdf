extern "C" %{
  /**
   * PLASMA include for defined and constants.
   *
   * @precisions normal z -> s d c
   *
   */
#include <lapacke.h>
#include <plasma.h>
#include <core_blas.h>

#define PRECISION_z
#define BLKLDD(_desc, _k) (_desc).mb

#include "dague.h"
#include "data_distribution.h"
#include "dplasmajdf.h"
#include "data_dist/matrix/matrix.h"

%}

/*
 * Globals
 */
uplo   [type = PLASMA_enum]
alpha  [type = Dague_Complex64_t]
beta   [type = Dague_Complex64_t]
descA  [type = "tiled_matrix_desc_t"]
A      [type = "dague_ddesc_t *"]

plasma_upper   [type="PLASMA_enum" hidden=on default=PlasmaUpper ]
plasma_lower   [type="PLASMA_enum" hidden=on default=PlasmaLower ]

LASET_L(m, n)

// Execution space
m = 1 .. ((uplo == plasma_upper) ? 0 : descA.mt-1)
n = 0 .. ( m < descA.nt ? m-1 : descA.nt-1 )

// Parallel partitioning
: A(m, n)

// Parameters

RW A <- A(m, n)
     -> A(m, n)
BODY
  int tempmm = ((m)==((descA.mt)-1)) ? ((descA.m)-(m*(descA.mb))) : (descA.mb);
  int tempnn = ((n)==((descA.nt)-1)) ? ((descA.n)-(n*(descA.nb))) : (descA.nb);
  int ldam = BLKLDD( descA, m );

        DRYRUN(
               LAPACKE_zlaset_work(
                   LAPACK_COL_MAJOR, 'L',
                   tempmm, tempnn, alpha, alpha, A, ldam);
               );

        printlog("thread %d laset( %d, %d )\n"
                 "    ( %s, %d, %d, %f, %f, A(%d,%d), %d)\n",
                 context->eu_id, m, n, "Lower",
                 tempmm, tempnn, alpha, alpha, m, n, ldam);
END

LASET_U(m, n)

// Execution space
m = 0   .. descA.mt-1
n = m+1 .. ((uplo == plasma_lower) ? 0 : descA.nt-1)

// Parallel partitioning
: A(m, n)

// Parameters

RW   A <- A(m, n)
       -> A(m, n)
BODY
  int tempmm = ((m)==((descA.mt)-1)) ? ((descA.m)-(m*(descA.mb))) : (descA.mb);
  int tempnn = ((n)==((descA.nt)-1)) ? ((descA.n)-(n*(descA.nb))) : (descA.nb);
  int ldam = BLKLDD( descA, m );

        DRYRUN(
               LAPACKE_zlaset_work(
                   LAPACK_COL_MAJOR, 'U',
                   tempmm, tempnn, alpha, alpha, A, ldam);
               );

        printlog("thread %d laset( %d, %d )\n"
                 "    ( %s, %d, %d, %f, %f, A(%d,%d), %d)\n",
                 context->eu_id, m, n, "Upper",
                 tempmm, tempnn, alpha, alpha, m, n, ldam);
END

LASET_DIAG(k)

// Execution space
     k = 0 .. ( descA.mt < descA.nt ? descA.mt-1 : descA.nt-1 )

// Parallel partitioning
: A(k, k)

// Parameters

RW  A <- A(k, k)
      -> A(k, k)
BODY
  int tempkm = ((k)==((descA.mt)-1)) ? ((descA.m)-(k*(descA.mb))) : (descA.mb);
  int tempkn = ((k)==((descA.nt)-1)) ? ((descA.n)-(k*(descA.nb))) : (descA.nb);
  int ldak = BLKLDD( descA, k );

        DRYRUN(
               LAPACKE_zlaset_work(
                   LAPACK_COL_MAJOR, lapack_const( uplo ),
                   tempkm, tempkn, alpha, beta, A, ldak);
               );

        printlog("thread %d laset( %d, %d )\n"
                 "    ( %s, %d, %d, %f, %f, A(%d,%d), %d)\n",
                 context->eu_id, k, k, lapack_const( uplo ),
                 tempkm, tempkn, alpha, beta, k, k, ldak);
END
extern "C" %{

%}
