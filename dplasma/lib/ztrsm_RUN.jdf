extern "C" %{
  /**
   * PLASMA include for defined and constants.
   *
   * @precisions normal z -> s d c
   *
   */
  
#include <plasma.h>
#include <core_blas.h>

#include "dague.h"
#include "data_distribution.h"
#include "data_dist/matrix/precision.h"
#include "dplasma/lib/dplasmajdf.h"

%}

/* Globals
 * GRIDrows, GRIDcols, NB, SIZE
 */
side [type = int]
uplo [type = int]
trans[type = int]
diag [type = int]

alpha[type = Dague_Complex64_t]

Am   [type = int]
An   [type = int]
Amb  [type = int]
Anb  [type = int]
Amt  [type = int]
Ant  [type = int]

Bm   [type = int]
Bn   [type = int]
Bmb  [type = int]
Bnb  [type = int]
Bmt  [type = int]
Bnt  [type = int]
     
/**************************************************
 *                      TRSM                      *
 **************************************************/
TRSM_IN(k)  [profile = off]
k = 0..Bnt-1

:A(k, k)

READ A <- A(k, k)
       -> A TRSM(0..Bmt-1, k)
BODY
 /* Nothing */
  printlog("thread %d ztrsm_in( %d )\n", 
           context->eu_id, k);
END

TRSM(m, k)

// Execution space
m = 0..Bmt-1
k = 0..Bnt-1

// Parallel partitioning
: B(m, k)

// Parameters
READ  A <- A TRSM_IN(k)
RW    B <- (k == 0) ? B(m, k) : C GEMM(k-1, m, k)
        -> k < Bnt-1 ? B GEMM(k, m, k+1..Bnt-1)
        -> B(m, k)

BODY
      Dague_Complex64_t lalpha = k == 0 ? alpha : (Dague_Complex64_t)1.0;    
      int tempmm = m == Bmt-1 ? Bm - m * Bmb : Bmb;
      int tempkn = k == Bnt-1 ? Bn - k * Bnb : Bnb;
      DRYRUN(
	   CORE_ztrsm(
			side, uplo, trans, diag,
			tempmm, tempkn,
			lalpha, A /*A(k, k)*/, Amb,
                                B /*B(m, k)*/, Bmb)
	   );

      printlog("thread %d CORE_ztrsm( %d, %d)\n\t( %s, %s, %s, %s, %d, %d, %f, A(%d,%d)[%p], %d, B(%d,%d)[%p], %d)\n",
	       context->eu_id, m, k,
               plasma_const( side ), plasma_const( uplo ), 
               plasma_const( trans ), plasma_const( diag ),
               tempmm, tempkn,
	       lalpha, k, k, A /*A(k, k)*/, Amb,
                       m, k, B /*B(m. k)*/, Bmb);
END

/**************************************************
 *                      GEMM                      *
 **************************************************/
GEMM_IN(k, n)  [profile = off]
k = 0..Bnt-2
n = k+1..Bnt-1

:A(k, n)
READ A <- A(k, n)
       -> A GEMM(k, 0..Bmt-1, n)
BODY
        /* Nothing */
        printlog("thread %d zgemm_in( %d, %d )\n", 
                 context->eu_id, k, n);
END

// Name
GEMM(k, m, n)

// Execution space
k = 0..Bnt-2
m = 0..Bmt-1
n = k+1..Bnt-1

// Parallel partitioning
: B(m, n)

// Parameters
READ  A <- A GEMM_IN(k, n)
READ  B <- B TRSM(m, k)
RW    C <- (k == 0)   ? B(m, n)      : C GEMM(k-1, m, n)
        -> (k+1 == n) ? B TRSM(m, n) : C GEMM(k+1, m, n)

BODY
      Dague_Complex64_t lalpha = k == 0 ? alpha : (Dague_Complex64_t)1.0;    
      int tempmm = m == Bmt-1 ? Bm - m * Bmb : Bmb;
      int tempnn = n == Bnt-1 ? Bn - n * Bnb : Bnb;
      DRYRUN(
	   CORE_zgemm(
			trans, PlasmaNoTrans,
                        tempmm, tempnn, Bmb,
			(Dague_Complex64_t)-1.0, B /* B(m, k)*/ , Bmb,
                                                 A /* A(k, n)*/ , Amb,
			lalpha,                  C /* B(m, n)*/ , Bmb)
	   );

      printlog("thread %d CORE_zgemm( %d, %d, %d )\n\t( %s, %s, %d, %d, %d, %f, B(%d,%d)[%p], %d, A(%d,%d)[%p], %d, %f, B(%d,%d)[%p], %d)\n",
	       context->eu_id,  k, m, n,
               plasma_const( trans ), plasma_const( PlasmaNoTrans ), 
               tempmm, tempnn, Bmb,
	       (Dague_Complex64_t)-1.0, m, k, B, Bmb,
                            k, n, A, Amb,
	       lalpha,      m, n, C, Bmb);
END
