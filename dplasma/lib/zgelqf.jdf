extern "C" %{
/*
 * Copyright (c) 2010-2013 The University of Tennessee and The University
 *                         of Tennessee Research Foundation. All rights
 *                         reserved.
 * Copyright (c) 2013      Inria. All rights reserved.
 * $COPYRIGHT
 *
 *
 * @precisions normal z -> s d c
 *
 */
#include "dplasma/lib/dplasmajdf.h"
#include "data_dist/matrix/matrix.h"

%}

descA  [type = "parsec_tiled_matrix_dc_t*"]
descT  [type = "parsec_tiled_matrix_dc_t*" aligned=descA]
ib     [type = "int"]
p_work [type = "parsec_memory_pool_t *" size = "(sizeof(parsec_complex64_t)*ib*descT->nb)"]
p_tau  [type = "parsec_memory_pool_t *" size = "(sizeof(parsec_complex64_t)   *descT->nb)"]


zgelqt_typechange(k)  [profile = off]
  /* Execution space */
  k = 0 .. %{ return dplasma_imin((descA->nt-1),(descA->mt-1)); %}

  : descA(k,k)

RW A <- A zgelqt(k)
     -> (k < (descA->mt-1)) ? A zunmlq(k, (k+1)..(descA->mt-1)) [type = UPPER_TILE]
     -> descA(k, k)                                              [type = UPPER_TILE]

BODY
{
    /* Nothing */
}
END

zgelqt(k)
  /* Execution space */
  k = 0 .. %{ return dplasma_imin((descA->nt-1),(descA->mt-1)); %}

  : descA(k,k)

  RW    A <- (0 == k) ? descA(k, k) : A2 ztsmlq(k-1, k, k)
          -> (descA->nt==(k+1)) ? descA(k,k)                         [type = LOWER_TILE]
          -> (descA->nt>=(k+2)) ? A1 ztslqt(k, k+1)               [type = LOWER_TILE]
          -> A zgelqt_typechange(k)

  RW    T <- descT(k, k)                                            [type = LITTLE_T]
          -> descT(k, k)                                            [type = LITTLE_T]
          -> (descA->mt-1 > k) ? T zunmlq(k, (k+1)..(descA->mt-1)) [type = LITTLE_T]

  /* Priority */
  ;(descA->mt-k)*(descA->mt-k)*(descA->mt-k)

BODY
{
    int tempkm = ((k)==(descA->mt-1)) ? (descA->m-(k*descA->mb)) : descA->mb;
    int tempkn = ((k)==(descA->nt-1)) ? (descA->n-(k*descA->nb)) : descA->nb;
    int ldak = BLKLDD( descA, k );

    printlog("CORE_zgeqrt(%d)\n"
             "\t(tempkm, tempkn, ib, A(%d,%d)[%p], ldak, T(%d,%d)[%p], descT->mb, p_elem_A, p_elem_B)\n",
             k, k, k, A, k, k, T);

#if !defined(PARSEC_DRY_RUN)

    void *p_elem_A = parsec_private_memory_pop( p_tau );
    void *p_elem_B = parsec_private_memory_pop( p_work );

    CORE_zgelqt(tempkm, tempkn, ib,
                A /* descA(k,k) */, ldak,
                T /* descT(k,k) */, descT->mb,
                p_elem_A, p_elem_B );

    parsec_private_memory_push( p_tau,  p_elem_A );
    parsec_private_memory_push( p_work, p_elem_B );

#endif  /* !defined(PARSEC_DRY_RUN) */
}
END


zunmlq(k,m)
  /* Execution space */
  k = 0   .. %{ return dplasma_imin((descA->nt-1),(descA->mt-2)); %}
  m = k+1 .. descA->mt-1

  : descA(m,k)

  READ  A <- A zgelqt_typechange(k)                       [type = UPPER_TILE]
  READ  T <- T zgelqt(k)                                  [type = LITTLE_T]
  RW    C <- (k == 0) ? descA(m, k) : A2 ztsmlq(k-1, k, m)
          -> (k == (descA->nt-1)) ? descA(m, k)
          -> (k <  (descA->nt-1)) ? A1 ztsmlq(k, k+1, m)

BODY
{
    int tempmm = ((m)==(descA->mt-1)) ? (descA->m-(m*descA->mb)) : descA->mb;
    int tempkn = ((k)==(descA->nt-1)) ? (descA->n-(k*descA->nb)) : descA->nb;
    int ldak = BLKLDD( descA, k );
    int ldam = BLKLDD( descA, m );

    printlog("CORE_zunmlq(%d, %d)\n"
             "\t(PlasmaRight, PlasmaConjTrans, tempmm, tempkn, tempkn, ib, A(%d,%d)[%p], ldak, T(%d,%d)[%p], descT->mb, A(%d,%d)[%p], ldam, p_elem_A, descT->nb)\n",
             k, m, k, k, A, k, k, T, m, k, C);

#if !defined(PARSEC_DRY_RUN)

    void *p_elem_A = parsec_private_memory_pop( p_work );

    CORE_zunmlq(PlasmaRight, PlasmaConjTrans,
                tempmm, tempkn, tempkn, ib,
                A /* descA(k,k) */, ldak,
                T /* descT(k,k) */, descT->mb,
                C /* descA(m,k) */, ldam,
                p_elem_A, descT->nb );

    parsec_private_memory_push( p_work, p_elem_A );

#endif  /* !defined(PARSEC_DRY_RUN) */
}
END


ztslqt_out_A0(k) [profile = off]
  k = 0..( (descA->nt <= descA->mt) ? descA->nt-2 : descA->mt-1 )

  : descA(k, k)

  RW A1 <- A1 ztslqt(k, descA->nt-1) [type = LOWER_TILE]
        -> descA(k, k)                 [type = LOWER_TILE]
BODY
{
    /* nothing */
}
END

ztslqt(k,n)
  /* Execution space */
  k = 0   .. %{ return dplasma_imin((descA->nt-2),(descA->mt-1)); %}
  n = k+1 .. descA->nt-1

  : descA(k, n)

  RW   A1 <- ( n == (k+1)       ) ? A zgelqt(n-1) : A1 ztslqt(k, n-1)       [type = LOWER_TILE]
          -> ( n == (descA->nt-1)) ? A1 ztslqt_out_A0(k) : A1 ztslqt(k, n+1) [type = LOWER_TILE]

  RW   A2 <- (k == 0) ? descA(k, n) : A2 ztsmlq(k-1, n, k)
          -> descA(k, n)
          -> (k < (descA->mt-1)) ? V ztsmlq(k, n, (k+1)..(descA->mt-1))

  RW   T  <- descT(k, n)                                                       [type = LITTLE_T]
          -> descT(k, n)                                                       [type = LITTLE_T]
          -> (k < (descA->mt-1)) ? T ztsmlq(k, n, (k+1)..(descA->mt-1))       [type = LITTLE_T]

    /* Priority */
; (descA->nt-k)*(descA->nt-k)*(descA->nt-k)

BODY
{
    int tempkm = ((k)==(descA->mt-1)) ? (descA->m-(k*descA->mb)) : descA->mb;
    int tempnn = ((n)==(descA->nt-1)) ? (descA->n-(n*descA->nb)) : descA->nb;
    int ldak = BLKLDD( descA, k );

    printlog("CORE_ztslqt(%d, %d)\n"
             "\t(tempkm, tempnn, ib, A(%d,%d)[%p], ldak, A(%d,%d)[%p], ldak, T(%d,%d)[%p], descT->mb, p_elem_A, p_elem_B)\n",
             k, n, k, k, T, k, n, A1, k, n, A2);

#if !defined(PARSEC_DRY_RUN)

    void *p_elem_A = parsec_private_memory_pop( p_tau );
    void *p_elem_B = parsec_private_memory_pop( p_work );

    CORE_ztslqt(tempkm, tempnn, ib,
                A1 /* descA(k,k) */, ldak,
                A2 /* descA(k,n) */, ldak,
                T  /* descT(k,n) */, descT->mb,
                p_elem_A, p_elem_B );

    parsec_private_memory_push( p_tau,  p_elem_A );
    parsec_private_memory_push( p_work, p_elem_B );

#endif  /* !defined(PARSEC_DRY_RUN) */
}
END


ztsmlq_out_A1(k, m) [profile = off]
  k = 0   .. %{ return dplasma_imin((descA->nt-2),(descA->mt-2)); %}
  m = k+1 .. descA->mt-1

  : descA(m, k)

  RW A1 <- A1 ztsmlq(k, descA->nt-1, m)
        -> descA(m, k)
BODY
{
    /* nothing */
}
END


ztsmlq(k,n,m)
  /* Execution space */
  k = 0     .. %{ return dplasma_imin((descA->mt-2),(descA->nt-2)); %}
  n = (k+1) .. (descA->nt-1)
  m = (k+1) .. (descA->mt-1)

  : descA(m, n)

  RW   A1 <- ( (k+1) == n ) ? C zunmlq(k, m) : A1 ztsmlq(k, n-1, m)
          -> ( n == (descA->nt-1) ) ? A1 ztsmlq_out_A1(k, m) : A1 ztsmlq(k, n+1, m)

  RW   A2 <- (0==k) ? descA(m, n) : A2 ztsmlq(k-1, n, m)
          -> (((k+1) == n) & ((k+1) == m)) ? A  zgelqt(m)            /* Diagonal */
          -> (((k+1) == m) & (    n > m )) ? A2 ztslqt(m, n)         /* Next column */
          -> (((k+1) == n) & (    m > n )) ? C  zunmlq(k+1, m)       /* Next row */
          -> (((k+1) <  n) & ((1+k) < m )) ? A2 ztsmlq(k+1, n, m)    /* trailing submatrix */

  READ V  <- A2 ztslqt(k, n)
  READ T  <- T  ztslqt(k, n)   [type = LITTLE_T]

    /* Priority */
; (descA->nt-k)*(descA->nt-m)*(descA->nt-m)

BODY
{
    int tempmm = ((m)==(descA->mt-1)) ? (descA->m-(m*descA->mb)) : descA->mb;
    int tempnn = ((n)==(descA->nt-1)) ? (descA->n-(n*descA->nb)) : descA->nb;
    int ldak = BLKLDD( descA, k );
    int ldam = BLKLDD( descA, m );

    printlog("CORE_ztsmlq(%d, %d, %d)\n"
             "\t(PlasmaRight, PlasmaConjTrans, tempmm, descA->nb, tempmm, tempnn, descA->mb, ib, A(%d,%d)[%p], ldam, A(%d,%d)[%p], ldam, A(%d,%d)[%p], ldak, T(%d,%d)[%p], descT->mb, p_elem_A, ldwork)\n",
             k, n, m, m, k, A1, m, n, A2, k, n, V, k, n, T);

#if !defined(PARSEC_DRY_RUN)
    void *p_elem_A = parsec_private_memory_pop( p_work );

    CORE_ztsmlq(PlasmaRight, PlasmaConjTrans,
                tempmm, descA->nb, tempmm, tempnn, descA->mb, ib,
                A1 /* descA(m,k) */, ldam,
                A2 /* descA(m,n) */, ldam,
                V  /* descA(k,n) */, ldak,
                T  /* descT(k,n) */, descT->mb,
                p_elem_A, descT->nb );

    parsec_private_memory_push( p_work, p_elem_A );

#endif  /* !defined(PARSEC_DRY_RUN) */
}
END
