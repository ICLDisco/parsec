extern "C" %{
/*
 * Copyright (c) 2018      The University of Tennessee and The University
 *                         of Tennessee Research Foundation.  All rights
 *                         reserved.
 */
#include "parsec/data_dist/matrix/matrix.h"
%}

/*
 * Globals
 */
uplo     [type = int]
descA    [type = "parsec_tiled_matrix_dc_t*"]
operation [type = "tiled_matrix_unary_op_t" ]
op_args  [type = "void *" ]

matrix_upper [type="int" hidden=on default=matrix_Upper ]
matrix_lower [type="int" hidden=on default=matrix_Lower ]

APPLY_L(m, n)  [profile = off]
  // Execution space
  m = 1 .. ((uplo == matrix_upper) ? 0 : descA->mt-1)
  n = 0 .. ( m < descA->nt ? m-1 : descA->nt-1 )

  // Parallel partitioning
  : descA(m, n)

  // Parameters
  RW    A <- descA(m, n)
          -> descA(m, n)

BODY
{
    operation( es, descA, A,
              matrix_UpperLower, m, n, op_args );

   
}
END

APPLY_U(m, n)  [profile = off]
  // Execution space
  m = 0   .. descA->mt-1
  n = m+1 .. ((uplo == matrix_lower) ? 0 : descA->nt-1)

  // Parallel partitioning
  : descA(m, n)

  // Parameters
  RW    A <- descA(m, n)
          -> descA(m, n)

BODY
{
    operation( es, descA, A,
              matrix_UpperLower, m, n, op_args );
}
END

APPLY_DIAG(k) [profile = off]
  // Execution space
  k = 0 .. ( descA->mt < descA->nt ? descA->mt-1 : descA->nt-1 )

  // Parallel partitioning
  : descA(k, k)

  // Parameters
  RW    A <- descA(k, k)
          -> descA(k, k)

BODY
{
    operation( es, descA, A,
              uplo, k, k, op_args );
}
END
